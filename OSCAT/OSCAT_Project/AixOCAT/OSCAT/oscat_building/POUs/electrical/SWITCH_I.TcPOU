<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.7">
  <POU Name="SWITCH_I" Id="{443f8cf1-eb22-47a2-b333-d215956a631f}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK SWITCH_I
VAR_INPUT
	set : BOOL;
	in : BOOL;
	rst : BOOL;
END_VAR
VAR_INPUT CONSTANT
	T_debounce : TIME := T#10ms;
	T_reconfig : TIME := T#1s;
	T_on_max : TIME := T#0h;
END_VAR
VAR_OUTPUT
	Q : BOOL;
END_VAR
VAR
	state : BYTE;
	edge : BOOL;
	r_edge : BOOL;
	T_on : TIME;
	tx: TIME;
END_VAR

(*
version 1.1	15 sep 2007
programmer 	oscat
tested by	oscat

this is an intelligent switch interface which has an autodetect feature for in and it will detect the type of switch connected automatically
the input can be connected to low active pulses, high active pulses and on/off switch.
it will automatically adjust to the correct type without manual adjustment
a configurable debounce timer will debounce input signals
if t_on_max is set to anything other then 0 the output will be turned off after the max on time is reached.

*)]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* read system time *)
tx := DWORD_TO_TIME(T_PLC_MS());

(* asynchronous set and reset first *)
IF set AND NOT rst THEN
	Q := TRUE;
	T_on := tx;
ELSIF rst THEN
	Q := FALSE;
(* check FOR input edge AND start debounce timer *)
ELSIF (in XOR edge) AND NOT (state = 1) THEN
	state := 1;
	T_on := Tx;
(* edge was detected and debounce time elapsed *)
ELSIF (state = 1) AND (Tx - t_debounce >= T_on) THEN
	state := 2;
	(* react on rising or falling edge depending on r_edge *)
	IF r_edge XOR in THEN Q := NOT Q; END_IF;
(* wait for T_reconfig and adjust r_edge *)
ELSIF state = 2 AND Tx - t_reconfig >= T_on THEN
	r_edge := in;
END_IF;
IF Q AND T_on_max > t#0ms AND Tx >= T_on + T_on_max THEN
	Q := FALSE;
END_IF;
edge := in;

(* revision history

hm	4 aug 2006	rev 1.0
	original version

hm	15.9.2007	rev 1.1
	replaced Time() with T_PLC_MS for compatibility and performance reasons

*)]]></ST>
    </Implementation>
    <LineIds Name="SWITCH_I">
      <LineId Id="34" Count="35" />
    </LineIds>
  </POU>
</TcPlcObject>