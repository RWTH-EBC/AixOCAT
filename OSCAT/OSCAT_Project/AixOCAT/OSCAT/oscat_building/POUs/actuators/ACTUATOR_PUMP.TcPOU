<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.7">
  <POU Name="ACTUATOR_PUMP" Id="{1fc8133a-d4c2-496b-88fc-1cce43149fbe}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK ACTUATOR_PUMP
VAR_INPUT
	IN : BOOL;
	MANUAL : BOOL;
	RST : BOOL;
END_VAR
VAR_INPUT CONSTANT
	MIN_ONTIME : TIME := t#10s;
	MIN_OFFTIME : TIME := t#10s;
	RUN_EVERY : TIME := t#10000m;
END_VAR
VAR_OUTPUT
	PUMP : BOOL;
END_VAR
VAR_IN_OUT
	RUNTIME : UDINT;
	CYCLES : UDINT;
END_VAR
VAR
	tx : TIME;
	last_change : TIME;
	meter : ontime;
	old_man : BOOL;
	init: BOOL;
END_VAR

(*
version 1.4		21. oct. 2008
programmer 		oscat
tested BY		oscat

actuator_pump is a pump interface which can be controlled by an input in and an input manual.
in or manual high turns the pump output on.
a rst input is used to reset the runtime counters.
when the pump was not active for a time run_every the actuator turns on the pump automatically for min_ontime.
two setup variables min_ontime and min_offtime guarantee a minimum runtime and offtime of the pump.
the units are hours and the output type is real to prevent from overfow.

*)]]></Declaration>
    <Implementation>
      <ST><![CDATA[tx := DWORD_TO_TIME(T_PLC_MS());

IF NOT init THEN
	init := TRUE;
	last_change := tx;
ELSIF rst THEN
	rst := FALSE;
	runtime := 0;
	cycles := 0;
ELSIF manual AND NOT pump AND NOT old_man THEN
	last_change := tx;
	pump := TRUE;
ELSIF NOT manual AND old_man AND pump AND NOT in THEN
	last_change := tx;
	pump := FALSE;
ELSIF in AND NOT pump AND tx - last_change >= min_offtime THEN
	last_change := tx;
	pump := TRUE;
ELSIF pump AND NOT in AND NOT manual AND tx - last_change >= min_ontime THEN
	last_change := tx;
	pump := FALSE;
ELSIF NOT pump AND (tx - last_change >= run_every) AND (run_every > T#0s) THEN
	last_change := tx;
	pump := TRUE;
END_IF;

meter(in := pump, seconds := runtime, cycles := cycles);
old_man := manual;



(*
hm	27.12.2006		rev 1.1
	fixed a failure while the pump would run for tmin after startup.

hm	15.9.2007		rev 1.2
	replaced Time() with T_PLC_MS for compatibility and performance reasons

hm	13. oct. 2008	rev 1.3
	auto activation can now be disabled when run_every = t#0s

hm	21. oct. 2008	rev 1.4
	changed to use ontime rev 2.0

*)]]></ST>
    </Implementation>
    <LineIds Name="ACTUATOR_PUMP">
      <LineId Id="39" Count="44" />
    </LineIds>
  </POU>
</TcPlcObject>