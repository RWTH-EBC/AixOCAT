<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.7">
  <POU Name="ACTUATOR_UD" Id="{eb8ffd52-9d74-4d82-893b-d387761d6c0f}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK ACTUATOR_UD
VAR_INPUT
	UD : BOOL;
	ON : BOOL;
	Manual : BOOL;
	Up : BOOL;
	Dn : BOOL;
	off : BOOL;
	YUP_in : BOOL;
	YDN_in : BOOL;
END_VAR
VAR_INPUT CONSTANT
	TON : TIME;
	TOFF : TIME;
	out_return : BOOL;
END_VAR
VAR_OUTPUT
	Yup : BOOL;
	Ydn : BOOL;
	status : BYTE;
END_VAR
VAR
	tx : TIME;
	last : TIME;
	init: BOOL;
END_VAR

(*
version 1.3	17. oct. 2008
programmer 	hugo
tested by		tobias

out_ud is a up/dn or Right/left locked output driver.
it can be controlled by automatic inputs UD and ON as well as manual inputs for up and dn.
a security input to shut off at any time is prvided by the off input.
two time set inputs ton and toff specify a minimum on time for outputs and a minimum off time before the next output can become active.
the module assures that only one output can be active at any time.
for higher security pls make sure you use hardware locking of the outputs to prevent from defective wiring or outputs.
further security is provided b feedback inputs Yup_in and Ydn_in which are separate inputs for the output signal, if these inputs are not available 
pls connect them directly to Yup and Ydn.
manual mode will override automatic mode by setting the manual input high.
a status output will be 0 for normal operation or 1 - 99 for error reporting and 101 - 199 for status reporting
The status output is ESR comptible.
*)
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* read system time *)
tx := DWORD_TO_TIME(T_PLC_MS());

IF NOT init THEN
	(* INITIALIZATION AT FIRST CALL *)
	last := tx;
	init := TRUE;
ELSIF off THEN
	(* emergency shut off detected *)
	Yup := FALSE;
	ydn := FALSE;
	last := tx;
	status := 101;

(* Watch FOR MIN TON TIME *)
ELSIF ((Yup OR Ydn) AND tx - last < Ton) THEN
	RETURN;
ELSIF NOT yup AND NOT ydn AND tx - last < toff THEN
	RETURN;

(* manual operation *)
ELSIF manual THEN
	status := 102;
	(* directional chage set both outs 0 first *)
	IF yup AND NOT up OR ydn AND NOT dn THEN
		ydn := FALSE;
		yup := FALSE;
		last := tx;
	(* manual up operation *)
	ELSIF UP AND NOT DN AND NOT OFF THEN
		Ydn := FALSE;
		Yup := TRUE;
		last := tx;
		status := 103;
	(* manual down operation *)
	ELSIF DN AND NOT UP AND NOT OFF THEN
		Yup := FALSE;
		Ydn := TRUE;
		last := tx;
		status := 104;
	(* maual mode but no up or down *)
	ELSE
		IF YUP OR YDN THEN LAST := tx; END_IF;
		Yup := FALSE;
		ydn := FALSE;
	END_IF;

(* automatic operation *)
ELSE
	(* directional change set outputs low first *)
	IF yup AND NOT ud OR ydn AND UD THEN
		yup := FALSE;
		ydn := FALSE;
		last := tx;
	(* automatic up operation *)
	ELSIF Ud AND on AND NOT OFF THEN
		Ydn := FALSE;
		Yup := TRUE;
		last := tx;
		status := 111;

	(* automatic down operation *)
	ELSIF NOT UD AND on AND NOT OFF THEN
		Yup := FALSE;
		Ydn := TRUE;
		last := tx;
		status := 112;
	(* standby mode *)
	ELSE
		IF Yup OR Ydn THEN last := tx; END_IF;
		Yup := FALSE;
		Ydn := FALSE;
		status := 110;
	END_IF;
END_IF;

(* make sure Yup and Ydn are never on at the same time *)
IF Ydn AND Yup_in AND out_return THEN
	Ydn := FALSE;
	status := 1;
END_IF;
IF Yup AND Ydn_in AND out_return THEN
	Yup := FALSE;
	status := 2;
END_IF;



(* revision history
hm 22. jan 2007	rev 1.1
	added status output
	changed inputs ton, toff to config variables
	added config var out_return to enable yup_in and Ydn_in
	manual mod will override automatic mode 

hm	15.9.2007	rev 1.2
	replaced Time() with T_PLC_MS for compatibility and performance reasons

hm	17. oct. 2008	rev 1.3
	deleted unnecessary initialization with 0
*)]]></ST>
    </Implementation>
    <LineIds Name="ACTUATOR_UD">
      <LineId Id="45" Count="100" />
    </LineIds>
  </POU>
</TcPlcObject>