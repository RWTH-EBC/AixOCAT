<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.7">
  <POU Name="DLOG_FILE_TO_SMTP" Id="{a9a210d9-0641-4425-a9e6-630c25e19793}" SpecialFunc="None">
    <Declaration><![CDATA[(* @OBJECTFLAGS := '0, 8' *)
FUNCTION_BLOCK DLOG_FILE_TO_SMTP
VAR_IN_OUT
	SERVER : STRING;
	MAILFROM : STRING;
	MAILTO : STRING(STRING_LENGTH);
	SUBJECT : STRING;
	BODY : STRING(STRING_LENGTH);
	X : DLOG_DATA;
END_VAR
VAR_INPUT
	FILE_DELETE : BOOL;
	TIMEOUT : TIME := T#30s;
	DTI : DT;
	DTI_OFFSET : INT;
	RETRY : INT;
	RETRY_TIME : TIME := T#30s;
	DNS_IP4 : DWORD;
END_VAR
VAR_OUTPUT
	DONE : BOOL;
	BUSY : BOOL;
	ERROR_C : DWORD;
	ERROR_T : BYTE;
END_VAR
VAR
	UCBD : UNI_CIRCULAR_BUFFER_DATA;
	UCB : UNI_CIRCULAR_BUFFER;
	SMTP_CLIENT : SMTP_CLIENT;
	WT_1 : TON;
	ftrig_old : BOOL;
	files : STRING(STRING_LENGTH);
	step : INT;
	smtp_busy : BOOL;
	smtp_done : BOOL;
	cnt : INT;

	(*LOG_MSG : LOG_MSG;
	_debug_enable : BOOL := TRUE;*)
END_VAR

(*
version 1.2		26. july. 2011
programmer		ks
tested by			ks

DLOG_FILE_TO_SMTP der Baustein versendet die vom DLOG_STORE_FILE_CSV erzeugten Dateien als Email
*)]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* neue Filenamen speichern *)
IF X.NEW_FILE_RTRIG AND ftrig_old = FALSE THEN
	UCBD.D_HEAD := 1;
	UCBD.D_MODE := 1;
	UCBD.D_STRING := X.NEW_FILE;
	UCB(DATA:=UCBD); (* Daten eintragen *)
END_IF;
ftrig_old := X.NEW_FILE_RTRIG;

CASE step OF

00:	IF UCBD.BUF_COUNT > 0 AND smtp_busy = FALSE THEN
		UCBD.D_MODE := 12; (* Daten lesen und löschen *)
		UCB(DATA:=UCBD);
		files := UCBD.D_STRING;
		IF FILE_DELETE THEN
			files := CONCAT(files,';#DEL#');
		END_IF;
		cnt := 0;
		step := 10;
	END_IF;

10:	IF smtp_busy = FALSE THEN
		IF smtp_done THEN
			(*####################  DEBUG-MESSAGE  ###################################*)
			(*IF _debug_enable THEN
				LOG_CL.NEW_MSG := 'F_T_SMTP: /FILE OK >~1<';
				LOG_CL.PRINTF[1] := files;
				LOG_MSG();
			END_IF;*)
			(*########################################################################*)
			step := 0;
		ELSE
			step := 20; (* pause nach error *)
		END_IF;
	END_IF;

20:	IF WT_1.Q THEN (* Wartezeit *)
		cnt := cnt - 1;
		IF RETRY = 0 THEN (* Transfer unbegrenzt wiederholen *)
			(*####################  DEBUG-MESSAGE  ###################################*)
			(*IF _debug_enable THEN
				LOG_CL.NEW_MSG := 'F_T_SMTP: Auftrag unbegrenzt wiederholen /file: ~2';
				LOG_CL.PRINTF[2] := files;
				LOG_MSG();
			END_IF;*)
			(*########################################################################*)
			step := 10; (* Transfer wiederholen *)
		ELSIF cnt >= 0 THEN
			(*####################  DEBUG-MESSAGE  ###################################*)
			(*IF _debug_enable THEN
				LOG_CL.NEW_MSG := 'F_T_SMTP: Auftrag wiederholen /cnt: ~1 /file: ~2';
				LOG_CL.PRINTF[1] := INT_TO_STRING(cnt);
				LOG_CL.PRINTF[2] := files;
				LOG_MSG();
			END_IF;*)
			(*########################################################################*)
			step := 10; (* Transfer wiederholen *)
		ELSE
			(*####################  DEBUG-MESSAGE  ###################################*)
			(*IF _debug_enable THEN
				LOG_CL.NEW_MSG := 'F_T_SMTP: Auftrag verworfen /file: ~2';
				LOG_CL.PRINTF[2] := files;
				LOG_MSG();
			END_IF;*)
			(*########################################################################*)
			step := 0; (* Auftrag verwerfen *)
		END_IF;
	END_IF;

END_CASE;

BUSY := step = 10;
DONE := step = 0;
WT_1(IN:= step = 20, PT:= RETRY_TIME);

SMTP_CLIENT(
	ACTIVATE:= BUSY,
	TIMEOUT:= TIMEOUT,
	DTI:= DTI,
	DTI_OFFSET:= DTI_OFFSET,
	DNS_IP4:= DNS_IP4,
	SERVER:= SERVER,
	MAILFROM:= MAILFROM,
	MAILTO:= MAILTO,
	SUBJECT:= SUBJECT,
	BODY:= BODY,
	FILES:= FILES,
	DONE=> smtp_done,
	BUSY=> smtp_busy,
	ERROR_C=> ERROR_C,
	ERROR_T=> ERROR_T
	);

(* revision history

ks	01. jan. 2011	rev 1.0
	original version

ks	28. apr. 2011	rev 1.1
	stringfehler bei option 'file_delete'

ks	26. july. 2011	rev 1.2
	Codeoptimierung D_MODE 12
*)]]></ST>
    </Implementation>
    <LineIds Name="DLOG_FILE_TO_SMTP">
      <LineId Id="48" Count="104" />
    </LineIds>
  </POU>
</TcPlcObject>