<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.7">
  <POU Name="NET_VAR_DWORD8" Id="{6c84c314-90e4-490a-917c-1165bb2ce4c7}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK NET_VAR_DWORD8
VAR_IN_OUT
	X : NET_VAR_DATA;
END_VAR
VAR_INPUT
	IN1 : DWORD;
	IN2 : DWORD;
	IN3 : DWORD;
	IN4 : DWORD;
	IN5 : DWORD;
	IN6 : DWORD;
	IN7 : DWORD;
	IN8 : DWORD;
END_VAR
VAR_OUTPUT
	OUT1 : DWORD;
	OUT2 : DWORD;
	OUT3 : DWORD;
	OUT4 : DWORD;
	OUT5 : DWORD;
	OUT6 : DWORD;
	OUT7 : DWORD;
	OUT8 : DWORD;
	ID : BYTE;
END_VAR
VAR
	init : BOOL;
	i : INT;
	i2 : INT;
	index : INT;
	index2 : INT;
	ra : ARRAY[1..8] OF DWORD;
END_VAR

(*
version 1.0		15. may. 2011
programmer		ks
tested by			ks

NET_VAR_DWORD8 überträgt bidirektional acht DWORD vom Master zum Client
*)
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF NOT init THEN
	init := TRUE;
	X.ID_MAX := X.ID_MAX + USINT#1;
	ID := USINT_TO_BYTE(X.ID_MAX);
END_IF;

IF X.STATE > BYTE#0 AND X.ERROR_ID = BYTE#0 THEN
	index := X.index; (* PCWORX Workaround *)
	IF INT_TO_UINT(index + 34) > X.BUF_SIZE THEN  (* ID (1) + TYPE (1) + 8 x DWORD (4) *)
		X.ERROR_ID := ID;
	ELSE
		IF X.STATE = BYTE#1 THEN (* Daten schreiben *)
			ra[1] := IN1;
			ra[2] := IN2;
			ra[3] := IN3;
			ra[4] := IN4;
			ra[5] := IN5;
			ra[6] := IN6;
			ra[7] := IN7;
			ra[8] := IN8;
			X.S_BUF.BUFFER[index] := ID; (* ID *)
			index := index + 1;
			X.S_BUF.BUFFER[index] := BYTE#13; (* TYPE DWORD8 *)
			index := index + 1;
			FOR i2 := 1 TO 8 DO;
				FOR i := 1 TO 4 DO;
					X.S_BUF.BUFFER[index] := DWORD_TO_BYTE(ra[i2]);
					ra[i2] := ROR(ra[i2],8);
					index := index + 1;
				END_FOR;
			END_FOR;

		ELSIF X.STATE = BYTE#2 THEN (* Daten lesen *)
			index2 := index + 1; (* PCWORX Workaround *)
			IF X.R_BUF.BUFFER[index] <> ID OR X.R_BUF.BUFFER[index2] <> BYTE#13 THEN (* TYPE DWORD8 *)
				X.ERROR_ID := ID;
			ELSE
				index := index + 2;
				FOR i2 := 1 TO 8 DO;
					ra[i2] := DWORD#0;
					FOR i := 1 TO 4 DO;
						ra[i2] := ra[i2] OR BYTE_TO_DWORD(X.R_BUF.BUFFER[index]);
						ra[i2] := ROR(ra[i2],8);
						index := index + 1;
					END_FOR;
				END_FOR;
				OUT1 := ra[1];
				OUT2 := ra[2];
				OUT3 := ra[3];
				OUT4 := ra[4];
				OUT5 := ra[5];
				OUT6 := ra[6];
				OUT7 := ra[7];
				OUT8 := ra[8];
			END_IF;
		END_IF;
		X.index := index;
	END_IF;
END_IF;

(* revision history
ks	15.may. 2011	rev 1.0
	original version

*)]]></ST>
    </Implementation>
    <LineIds Name="NET_VAR_DWORD8">
      <LineId Id="42" Count="64" />
    </LineIds>
  </POU>
</TcPlcObject>