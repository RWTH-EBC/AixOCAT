<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.7">
  <POU Name="TN_SC_ADD_SHADOW" Id="{50dd60e7-d782-4bfe-a6b7-70819e036433}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK TN_SC_ADD_SHADOW
VAR
	in_Offset : INT;
	in_X : INT;
	in_Y : INT;
END_VAR
VAR_INPUT
	Iin_Y1 : INT;
	Iin_X1 : INT;
	Iin_Y2 : INT;
	Iin_X2 : INT;
	Iin_OPTION : INT;
END_VAR
VAR_IN_OUT
	Xus_TN_SCREEN : us_TN_SCREEN;
END_VAR

(*
version 1.0		10. mai 2010
programmer		ks
tested by		ks

TN_SC_ADD_SHADOW zeichnet auf der linken und unteren Seite eines rechteckigen Bereiches einen Schatten
*)
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF TN_SC_XY2_ERROR(Iin_X1,Iin_Y1,Iin_X2,Iin_Y2) THEN RETURN; END_IF;

IF Iin_Y1 < 0 OR Iin_X1 < 0 OR Iin_Y2 < 0 OR Iin_X2 < 0 THEN RETURN; END_IF; (* min werte prüfen *)
IF Iin_Y1 > 23 OR Iin_X1 > 79 OR Iin_Y2 > 23 OR Iin_X2 > 79 THEN RETURN; END_IF; (* max werte prüfen *)

in_Offset := (Iin_Y1 * 80) + Iin_X2;

IF Iin_OPTION = INT#00 THEN
	(* Schatten durch Farbanpassung erzeugen *)
	FOR in_Y := Iin_Y1 TO Iin_Y2 DO
		Xus_TN_SCREEN.bya_COLOR[in_Offset] := TN_SC_SHADOW_ATTR(Xus_TN_SCREEN.bya_COLOR[in_Offset]);
		in_Offset := in_Offset + 80;
		Xus_TN_SCREEN.bya_Line_Update[in_Y] := TRUE; (* Zeile für Update eintragen *)
	END_FOR;
ELSE
	(* Schatten durch Farbe Schwarz und Leerzeichen erzeugen *)
	FOR in_Y := Iin_Y1 TO Iin_Y2 DO
		Xus_TN_SCREEN.bya_COLOR[in_Offset] := BYTE#00;
		Xus_TN_SCREEN.bya_CHAR[in_Offset] := BYTE#32;
		in_Offset := in_Offset + 80;
		Xus_TN_SCREEN.bya_Line_Update[in_Y] := TRUE; (* Zeile für Update eintragen *)
	END_FOR;
END_IF;

in_Offset := (Iin_Y2 * 80) + Iin_X1;

IF Iin_OPTION = INT#00 THEN
	(* Schatten durch Farbanpassung erzeugen *)
	FOR in_X  := Iin_X1 TO Iin_X2 DO
		Xus_TN_SCREEN.bya_COLOR[in_Offset] := TN_SC_SHADOW_ATTR(Xus_TN_SCREEN.bya_COLOR[in_Offset]);
		in_Offset := in_Offset + 1;
	END_FOR;
ELSE
	(* Schatten durch Farbe Schwarz und Leerzeichen erzeugen *)
	FOR in_X  := Iin_X1 TO Iin_X2 DO
		Xus_TN_SCREEN.bya_COLOR[in_Offset] := BYTE#00;
		Xus_TN_SCREEN.bya_CHAR[in_Offset] := BYTE#32;
		in_Offset := in_Offset + 1;
	END_FOR;
END_IF;

(* revision history
ks	10. mai. 2010	rev 1.0
	original version
*)]]></ST>
    </Implementation>
    <LineIds Name="TN_SC_ADD_SHADOW">
      <LineId Id="25" Count="44" />
    </LineIds>
  </POU>
</TcPlcObject>