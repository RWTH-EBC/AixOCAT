<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.7">
  <POU Name="ARRAY_VAR" Id="{e34ba509-c711-4875-9b6b-618099e85b30}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION ARRAY_VAR : REAL
VAR_INPUT
	pt : POINTER TO ARRAY[0..32000] OF REAL;
	size : UINT;
END_VAR
VAR
	avg : REAL;
	i: UINT;
	stop: UINT;
END_VAR

(*
version 1.3	10. mar. 2009
programmer 	hugo
tested by		tobias

this function will calculate the variance of a given array.
the function needs to be called:	array_var(adr("array"),sizeof("array"));
because this function works with pointers its very time efficient and it needs no extra memory.

*)]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* at first we calualte the arithmetic average of the array *)

stop := SHR(size,2)-1;
avg := pt^[0];
FOR i := 1 TO stop DO
	avg := avg + pt^[i];
END_FOR;
avg := avg / UINT_TO_REAL(stop + 1);

(* in a second run we calculate the variance of the array *)

array_var := (pt^[0] - avg) * (pt^[0] - avg);
FOR i := 1 TO stop DO
	array_var := array_var + (pt^[i] - avg) * (pt^[i] - avg);
END_FOR;
ARRAY_VAR := ARRAY_VAR / UINT_TO_REAL(stop);

(* revision history
hm 	1.4.2007	rev 1.0
	function created

hm	12.12.2007	rev 1.1
	changed code for better performance

hm	16. mar. 2008	rev 1.2
	changed type of input size to uint

hm	10. mar. 2009	rev 1.3
	added type conversions for compatibility reasons
*)]]></ST>
    </Implementation>
    <LineIds Name="ARRAY_VAR">
      <LineId Id="21" Count="29" />
    </LineIds>
  </POU>
</TcPlcObject>