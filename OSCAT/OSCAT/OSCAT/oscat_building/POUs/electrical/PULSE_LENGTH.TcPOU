<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.7">
  <POU Name="PULSE_LENGTH" Id="{98733a49-d71e-4d1d-8ac0-6307310c7d18}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK PULSE_LENGTH
VAR_INPUT
	in : BOOL;
END_VAR
VAR_OUTPUT
	short : BOOL;
	middle : BOOL;
	long : BOOL;
END_VAR
VAR_INPUT CONSTANT
	T_short : TIME := t#100ms;
	T_long : TIME := t#1s;
END_VAR
VAR
	tx : TIME;
	tn: TIME;
	edge : BOOL;
END_VAR

(*
version 1.1	15 sep 2007
programmer 	oscat
tested by	oscat

Pulse_length indicates on 3 outputs if an input pulse had been shorter than T_short (short = True), longer than T_long (long = True) or between T_short and T_long (middle = True).

*)]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* read system time *)
tx := DWORD_TO_TIME(T_PLC_MS());

(* reset outputs, they should only be active 1 cycle *)
short := FALSE;
Middle := FALSE;
long := FALSE;

(* detect rising edge *)
IF in AND NOT edge THEN
	edge := TRUE;
	tn := tx;

(* detect for falling edge *)
ELSIF NOT in AND edge THEN
	edge := FALSE;
	tn := tx - tn;
	IF tn < t_short THEN
		short := TRUE;
	ELSIF tn > t_long THEN
		long := TRUE;
	ELSE
		Middle := TRUE;
	END_IF;

(* generate long pulse as soon as T_long is reached *)
ELSIF in AND tx - tn > t_long THEN
	long := TRUE;
END_IF;


(* revision history

hm	15. feb 2006	rev 1.0
	original version

hm	15 sep 2007		rev 1.1
	replaced Time() with T_PLC_MS for compatibility and performance reasons

*)]]></ST>
    </Implementation>
    <LineIds Name="PULSE_LENGTH">
      <LineId Id="27" Count="39" />
    </LineIds>
  </POU>
</TcPlcObject>