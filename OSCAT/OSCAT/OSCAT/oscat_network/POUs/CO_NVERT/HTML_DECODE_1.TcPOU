<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.0">
  <POU Name="HTML_DECODE_1" Id="{a2b7675d-6b73-4e14-9e6d-16c6d395f58b}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION HTML_DECODE_1 : STRING(STRING_LENGTH)
VAR_INPUT
	IN : STRING(STRING_LENGTH);
END_VAR
VAR
	pos : INT;
	end : INT;
	tmp : STRING(10);
	code : STRING(1);
END_VAR

(*
version 1.3		10. may 2010
programmer 	hugo
tested by		oscat

HTML_DECODE converts HTML coded characters to text. 
&amp; is replaced by & ("Ampersand")
&lt; is replaced by < ("lower than")
&gt; is replaced by > ("greater than")
&quot; is replaced by " ("Quote")
all other &names characters are replaced by their respective character codes.

*)]]></Declaration>
    <Implementation>
      <ST><![CDATA[HTML_DECODE := IN;

(* docode characters encoded by character position *)
pos := FIND(HTML_DECODE, '&');
WHILE pos > 0 DO
	tmp := MID(HTML_DECODE, 2, pos + 1);
	IF tmp = '#x' OR tmp = '#X' THEN
		(* hexadecimal representation found *)
		tmp := MID(HTML_DECODE, 10, pos + 3);
		end := FIND(tmp, ';');
		CODE := CHR_TO_STRING(DWORD_TO_BYTE(HEX_TO_DWORD(LEFT(tmp, end - 1))));
		HTML_DECODE := REPLACE(HTML_DECODE, CODE, end + 3, pos);
	ELSIF LEFT(tmp,1) = '#' THEN
		tmp := MID(HTML_DECODE, 10, pos + 2);
		end := FIND(tmp, ';');
		CODE := CHR_TO_STRING(INT_TO_BYTE(STRING_TO_INT(LEFT(tmp, end - 1))));
		HTML_DECODE := REPLACE(HTML_DECODE, CODE, end + 2, pos);
	ELSE
		(* replace all named characters *)
		tmp := MID(HTML_DECODE, 10, pos + 1);
		end := FIND(tmp, ';');
		CODE := CHR_TO_STRING(CHARCODE(LEFT(tmp, end - 1)));
		HTML_DECODE := REPLACE(HTML_DECODE, CODE, end + 1, pos);
	END_IF;
	(* search for next occurence of encoded characters *)
	pos := FINDP(HTML_DECODE, '&', pos + 1);
END_WHILE;



(* revision history
hm	3. mai. 2008	rev 1.0
	original version

hm	16. jun. 2008	rev 1.1
	changed nested call of replace for better compatibility

hm	17. dec. 2008	rev 1.2
	changed function CHR to CHR_TO_STRING

hm	10. may. 2010	rev 1.3
	stringlength 250

*)]]></ST>
    </Implementation>
    <LineIds Name="HTML_DECODE_1">
      <LineId Id="24" Count="43" />
    </LineIds>
  </POU>
</TcPlcObject>