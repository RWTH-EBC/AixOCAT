<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.0">
  <POU Name="ELEMENT_GET" Id="{8cf87344-054e-45cd-bb9a-2f4aae5f2f13}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION ELEMENT_GET : STRING(ELEMENT_LENGTH)
VAR_INPUT
	SEP : BYTE;
	POS : INT;
END_VAR
VAR_IN_OUT
	ELEMENT : STRING(ELEMENT_LENGTH);
END_VAR
VAR
	i : INT := 1;
	o : INT := 1;
	pt : POINTER TO ARRAY[1..ELEMENT_LENGTH] OF BYTE;
	po : POINTER TO ARRAY[1..ELEMENT_LENGTH] OF BYTE;
	cnt: INT;
	c: BYTE;
END_VAR

(*
version 1.0	05. apr. 2011
programmer 	ks
tested by		kst

ELEMENT_GET liefert das element an der stelle pos einer liste.
die liste liegt als string von elementen vor die mit den zeichen SEP separiert sind.

*)
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* load pointers *)
pt := ADR(ELEMENT);
po := ADR(ELEMENT_GET);

(* search for the n-th occurence of a separation character and store position in i *)
WHILE (cnt < pos) AND (i < ELEMENT_LENGTH) AND (pt^[i] > 0) DO
	IF pt^[i] = sep THEN cnt := cnt + 1; END_IF;
	i := i + 1;
END_WHILE;

(* copy the list element to output *)
IF i < ELEMENT_LENGTH THEN c := pt^[i]; END_IF;
WHILE (c <> sep) AND (c > 0) AND (o < ELEMENT_LENGTH) AND (i < ELEMENT_LENGTH) DO
	po^[o] :=pt^[i];
	o := o + 1;
	i := i + 1;
	c := pt^[i];
END_WHILE;

(* terminate the output string *)
po^[o] := 0;

(* revision histroy
ks 05. apr. 2011	rev 1.0
	original release

*)]]></ST>
    </Implementation>
    <LineIds Name="ELEMENT_GET">
      <LineId Id="27" Count="26" />
    </LineIds>
  </POU>
</TcPlcObject>