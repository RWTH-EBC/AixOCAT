<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.0">
  <POU Name="BLIND_SHADE" Id="{5e98910f-9151-4bf7-a976-2236f704c33c}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK BLIND_SHADE
VAR_INPUT
	UP, DN : BOOL;
	S_IN : BYTE;
	PI, AI : BYTE;
	ENABLE : BOOL;
	SUN : BOOL;
END_VAR
VAR_IN_OUT
	CX : CALENDAR;
END_VAR
VAR_INPUT CONSTANT
	sunrise_offset : TIME := T#1h;
	sunset_preset : TIME := T#1h;
	direction : REAL := 180.0;
	angle_offset : REAL := 80.0;
	slat_width : REAL := 80.0;
	Slat_spacing : REAL := 60.0;
	Shade_delay : TIME := T#60s;
	Shade_pos : BYTE;
END_VAR
VAR_OUTPUT
	QU, QD : BOOL;
	STATUS : BYTE;
	PO, AO : BYTE;
END_VAR
VAR
	angle: REAL;
	sun_delay : TOF;
END_VAR

(*
version 1.4	13. mar. 2009
programmer 	hugo
tested by		tobias


*)
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* the input sun is sent through tof which will delay the shade for the time shade_delay *)
sun_delay(IN := sun, PT := shade_delay);

IF UP AND DN AND enable AND sun_delay.Q AND cx.SUN_HOR > direction - angle_offset AND cx.SUN_HOR < direction + angle_offset AND
	DT_TO_TOD(cx.UTC) > cx.SUN_RISE + sunrise_offset AND DT_TO_TOD(cx.UTC) < cx.SUN_SET - sunset_preset THEN
	status := 151;
	QU := UP;
	QD := DN;
	(* position is predefined *)
	po := shade_pos;
	(* shading is active now calculate the slat angle *)
	(* calculate the max angle for the blind *)
	angle := DEG(ATAN(slat_spacing / slat_width));
	(* check if sun angle is between 0 and max angle *)
	IF cx.SUN_VER > 0.0 AND cx.SUN_VER < angle THEN
		angle := cx.SUN_VER + DEG(ACOS(COS(RAD(cx.SUN_VER))*slat_spacing / Slat_width));
		ao := DINT_TO_BYTE(LIMIT(0,TRUNC(angle * 2.833333333), 255));
	ELSE
		ao := 255;
	END_IF;
ELSE
	QU := UP;
	QD := DN;
	po := pi;
	ao := ai;
	status := S_IN;
END_IF;

(* revision history
hm	15 oct. 2007	rev 1.0
	original version

hm	19. oct 2007	rev 1.1
	manual mode was not detected because outputs were checked and not inputs UP and DN

hm	24. nov 2007	rev 1.2
	added shade_delay to avoid constant up and down if sun is out parcially

hm	8. feb 2009	rev 1.3
	added shading position shade_pos to be configured
	changed inputs to utilize calendar data structure

hm	13. mar. 2009	rev 1.4
	improved code

*)]]></ST>
    </Implementation>
    <LineIds Name="BLIND_SHADE">
      <LineId Id="39" Count="45" />
    </LineIds>
  </POU>
</TcPlcObject>